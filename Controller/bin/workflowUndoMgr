#!/usr/bin/perl

use strict;
use lib "$ENV{GUS_HOME}/lib/perl";
use File::Temp qw(tempfile);

usage() unless scalar(@ARGV) == 3 && $ARGV[0] eq '-h'
	 && ($ARGV[2] eq '-r' || $ARGV[2] eq '-t');

my $home_dir = $ARGV[1];
my $runOrTest = $ARGV[2];

# Read steps from STDIN
my @steps;
while(<STDIN>) {
  chomp;
  s/\s+$//g; # lose trailing spaces
  /^\S+$/ || die "invalid step name: '$_'\n";
  push(@steps, $_);
}

die "\nNo steps provided on stdin... nothing to undo!\n\n" unless scalar(@steps);

print STDERR "The following steps will be undone:\n\n";
map {print STDERR "$_\n";} @steps;

print STDERR "\nYou have 10 seconds to hit ctrl-C if that is not ok!\n\n";
sleep(10);

print STDERR "OK, here we go...\n\n";

# Create a temporary file with the step names
my ($fh, $filename) = tempfile(UNLINK => 1);
foreach my $step (@steps) {
  print $fh "$step\n";
}
close($fh);

# Make a single call to workflow with the temp file
my $cmd = "workflow -h $home_dir $runOrTest -u $filename";
print STDERR "$cmd\n";
system($cmd);
my $status = $? >> 8;

if ($status) {
  print STDERR "Error.  Failed with status '$status': $!\n\n";
  exit($status);
} else {
  print STDERR "Done\n\n";
  exit(0);
}

sub usage {
  print "
Run a series of undos.  Takes names of steps on STDIN.  Each provided
step name will be undone in a single workflow invocation.

Use the workflow command's -s1 option to find all DONE steps, and pipe
those to egrep to find the ones you want to undo.  Pipe those to
workflowUndoMgr.  (see Usage below).

Or, manually create a file of step names and cat it to workflowUndoMgr

Usage: workflow -h workflow_home_dir -s1 DONE | egrep some_regex_here | workflowUndoMgr -h workflow_home_dir <-r|-t>

";

  exit(1);
}
